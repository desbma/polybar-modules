# Maintainer: desbma
# shellcheck disable=SC2034,SC2148,SC2154,SC2164
pkgname=polybar-modules
pkgver=2025.6.8
pkgrel=1
pkgdesc='Custom modules for Polybar'
arch=('x86_64')
url="https://github.com/desbma/${pkgname}"
license=('GPL')
depends=('gcc-libs' 'openssl')
optdepends=('arch-audit: for Arch updates module'
            'bluez-utils: for Bluetooth module'
            'libpulse: for PulseAudio module'
            'nvidia-utils: for Nvidia GPU module'
            'pacman-contrib: for Arch updates module'
            'playerctl: for player module'
            'syncthing: for Syncthing module'
            'xidlehook: for autolock module'
            'yay: for Arch updates module')
makedepends=('cargo' 'git')
source=("git+https://github.com/desbma/${pkgname}")
sha512sums=('SKIP')

pkgver() {
    date +%Y.%-m.%-d
}

prepare() {
    cd "${pkgname}"
    cargo fetch --locked
}

build() {
    cd "${pkgname}"
    cargo build --frozen --release
}

check() {
    cd "${pkgname}"
    cargo test --frozen
}

package() {
    cd "${pkgname}"
    install -Dm 755 -t "${pkgdir}/usr/bin" ./target/release/${pkgname}
    install -Dm 644 arch-linux/${pkgname}.hook "${pkgdir}/usr/share/libalpm/hooks/zz40-${pkgname}.hook"
}
